#!/usr/bin/env perl
#
# Wrapper around pegasus-submit-dag to run a workflow
#
# Usage: pegasus-run rundir
##
#  Copyright 2007-2010 University Of Southern California
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#  http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing,
#  software distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License.
##
#
# Author: Jens-S. VÃ¶ckler voeckler at isi dot edu
# Author: Gaurang Mehta gmehta at isi dot edu
# Revision : $Revision$
#
use 5.006;
use strict;
use Carp;
use File::Spec;
use File::Basename qw(basename dirname);
use Getopt::Long qw(:config bundling no_ignore_case);

# path to load our libs.. 
BEGIN { 
    require File::Spec->catfile( dirname($0), 'common.pm' );
    common->import(':all');
}

# load our own local modules
use Work::Common;
use Work::Properties qw(%initial); # parses -Dprop=val from @ARGV


# constants
sub usage(;$);			# { }
$main::DEBUG = 0;		# for now
my $condor;			# if set, use Condor for pegasus-monitord
my $grid=0;                     # if set, disable grid checks;
my $properties="";  	        # properties file for Pegasus
my $default_tsd = File::Spec->catfile( File::Spec->rel2abs( 
    File::Spec->canonpath(dirname($0)) ), 'pegasus-monitord' );
my $dagman=File::Spec->catfile( File::Spec->rel2abs(
				    File::Spec->canonpath(dirname($0)) ), 'pegasus-dagman' );
    
my $database=1  ;
my $tsd;
my $monitor;
GetOptions( "help|h" => \&usage,
	    "monitor|m=s" => \$tsd,
	    "database!"=>\$database,
	    "condor" => \$condor,
	    "debug|d=o" => \$main::DEBUG,
	    "verbose|v+" => \$main::DEBUG,
            "grid!"=>\$grid,
	    "M" => \$monitor,
	    "conf|c=s"=> \$properties
	    );

# arguments
my $run = shift || usage(1);
usage(1) unless -d $run;
my %config = slurp_braindb( $run ) or die "ERROR: open braindb: $!\n";
# this assumes karan writes a merged system + user properties per workflow in the dag dir.
my $newprops = {'pegasus.properties'=> $config{properties}};
$newprops->{'pegasus.user.properties'}=$properties if defined $properties;
my $props = Work::Properties->new($newprops);

$tsd = $tsd || $props->property('pegasus.monitord') || $default_tsd;

#
# --- functions -------------------------------------------------
#

sub usage(;$) {
    my $ec = shift || 0;
    my $basename = basename($0,'.pl');
    print << "EOF";

Usage: $basename [options] rundir

Mandatory arguments:
  rundir  is the directory where the workflow resides as well as all
          ancilliary files related to the workflow.

Optional arguments:
 -Dprop=val         explicit settings of a property (multi-option). 
 -h|--help          print this help message and exit.
 -m|--monitor l     uses the workflow monitor daemon installed as l, default
                    is $default_tsd.
 -d|--debug lvl     sets the debug level (verbosity), default is $main::DEBUG. 
 -v|--verbose       raises debug level by 1, see --debug. 
 --condor           uses Condor to submit daemons (prototype).
 --database|--nodatabase Make tailstad log events in the workflow database (default yes)
 --grid|--nogrid  Disable or Enable grid checks (default is disabled)
 -c|--conf fn	    use file fn as pegasus properties file.
EOF
    exit($ec);
}

sub create_args {
    # purpose: create either a shell daemon, or condor-submitted job
    # warning: It is assumed that condor-jobs require -N to foreground
    # globals: $condor (IN): use Condor (true) or daemon (false) mode
    # paramtr: $daemon (IN): executable location of daemon 
    #          @args (IN): other arguments to daemon
    # returns: argument vector ready to execute
    my $daemon = shift || croak "Need a daemon name";

    my @result;
    if ( $condor ) {
	# use Condor to submit a daemon -- makes it restartable on SH death
	# however, insert -N into the daemon's argument list for foregrounding
	my $condor_submit = find_exec('condor_submit') || 
	    die "ERROR: Unable to find condor_submit\n";
	my $base = 'pegasus-run-' . basename($daemon);
	my $fn = File::Spec->catfile( $run, "$base.sub" );
	local(*SUB);
	open( SUB, ">$fn" ) || die "open $fn: $!\n";

	# only arg
	@result = ( );
	foreach my $k ( keys %initial ) { push( @result, "-D$k=$initial{$k}" ) }
	push( @result, '-N' );
	push( @result, @_ ) if ( @_ > 0 );

	# write submit file for (restartable) pegasus-monitord
	print SUB "initialdir = $run\n";
	print SUB 'executable = ', File::Spec->rel2abs($daemon), "\n";
	print SUB 'arguments  = ', join(' ',@result), "\n";
	print SUB "universe   = local\n";
	print SUB "notification = NEVER\n";
	print SUB "getenv = True\n";
	print SUB "output = $base.out.txt\n";
	print SUB "error  = $base.err.txt\n";
	print SUB "log    = $base.log.txt\n";
	print SUB "queue\n";
	close SUB;
	@result = ( $condor_submit, $fn );
    } else {
	# run daemon directly
	@result = ( $daemon );
	foreach my $k ( keys %initial ) { push( @result, "-D$k=$initial{$k}" ) }
	push( @result, @_ ) if ( @_ > 0 );
    }

    @result;
}


#
# --- main ------------------------------------------------------
#

# sanity check: lower umask
umask 0002;

#my $fin=$config{jsd};
#open( F, ">>$fin" ) && close F;

# if brain-dump failed, this will be undefined
# if the user corrupted things, this will not be readable
#die "ERROR: Unable to determine job state log\n" 
#    unless ( $config{jsd} && -r $config{jsd} );

# where were we...
my $here = File::Spec->curdir();
$SIG{'__DIE__'} = sub {
    chdir($here) if defined $here;
};
chdir($run) || die "ERROR: chdir $run: $!\n";

# sanity check: find the pegasus-monitord daemon
warn "WARN: Either $tsd does not exist or is not executable\n" unless -x $tsd;
print STDERR "# found $tsd\n" if $main::DEBUG;

# Do GRID check if $grid enabled
if ( $grid ) {
    # sanity check: Is there a GLOBUS_LOCATION?

    die ( "ERROR: Your environment setup misses GLOBUS_LOCATION.\n",
	  "Please check carefully that you have sourced the correct setup files!\n" )
	unless exists $ENV{'GLOBUS_LOCATION'};

    # sanity check: find grid-proxy-init from GLOBUS_LOCATION
    my $g_l = $ENV{'GLOBUS_LOCATION'};
    print STDERR "# GLOBUS_LOCATION=$g_l\n" if $main::DEBUG;

    # sanity check: Is G_L part of L_L_P?
    my @llp = grep { /^$g_l/ } split /:/, $ENV{'LD_LIBRARY_PATH'};
    $ENV{'LD_LIBRARY_PATH'}=File::Spec->catfile($ENV{'GLOBUS_LOCATION'},"/lib") if @llp == 0;

    # Find grid-proxy-init (should we use openssl instead?? )
    my $gpi = File::Spec->catfile( $g_l, 'bin', 'grid-proxy-info' );
    die "ERROR: Unable to find $gpi\n" unless -x $gpi;
    print STDERR "# found $gpi\n" if $main::DEBUG;

    # common user error
    # sanity check: Sufficient time left on grid proxy certificate
    open( GPI, "$gpi -timeleft 2>&1|" ) || die "open $gpi: $!\n";
    my $timeleft = <GPI>;
    chomp($timeleft);
    $timeleft += 0;			# make numeric
    close GPI;
    die( "ERROR: $gpi died on signal ", ($? & 127) ) if ( ($? & 127) > 0 );
    die( "ERROR: Grid proxy not initialized, Please generate a new proxy\n" ) if $timeleft == -1;
    die( "ERROR: Grid proxy expired, please refresh\n" ) if $timeleft == 0;
    die( "ERROR: $gpi exited with status ", $?>>8 ) if ( $? != 0 );
    warn( "ERROR: Too little time left ($timeleft s) on grid proxy. Please refresh your proxy\n" ) 
	if $timeleft < 7200;
    print STDERR "# grid proxy has $timeleft s left\n" if $main::DEBUG;
} # end if($grid) checks only if grid option is enabled.

if ( $config{dag} ) {
    # find pegasus-submit-dag
    my $psd = File::Spec->catfile( $config{pegasushome}, 'bin', 'pegasus-submit-dag' );
    die "ERROR: Unable to access $psd\n" unless -x $psd;
    print STDERR "# found $psd\n" if $main::DEBUG;

    # sanity check: Is the DAG file there? 
    die "ERROR: Unable to locate $config{dag}\n" unless -r $config{dag};
    print STDERR "# found $config{dag}\n" if $main::DEBUG;

    # NEW: is there a rescue file, or multiple rescue levels?
    my $original;
    my @rescue = check_rescue($run,$config{dag});
    if ( @rescue > 0 ) {
	my (@stat,%rescue,$maxsize);
	foreach my $fn ( @rescue ) {
	    if ( (@stat = stat($fn)) > 0 ) {
		$rescue{$fn} = [ @stat ];
		$maxsize = $stat[7] if $maxsize < $stat[7];
	    }
	}

	print "\n\nDetected the presence of Rescue DAGs:\n";
	my $width = log10($maxsize);
	foreach my $fn ( @rescue ) {
	    printf( " %s %*u %s\n", 
		    isodate($rescue{$fn}[9]), 
		    $width, $rescue{$fn}[7], 
		    basename($fn) );
	}

	# overwrite with "latest" (read: longest basename) rescue DAG
	$original = $config{dag};
	$config{dag} = $rescue[$#rescue];
	print "\nWILL USE ", $config{dag}, "\n\n";
    }

    # find the workflow name and timestamp for pegasus-status
    my $workflow=$config{pegasus_wf_name};
    my $time=$config{timestamp};

    # start DAGMan with default throttles
    my @extra = ();
    foreach my $k ( keys %initial ) {
	push( @extra, "-D$k=$initial{$k}" );
    }

    my @args = ( $psd );
    push( @args, @extra ) if @extra > 0;
    push( @args, '-d', 0+$main::DEBUG);
    push( @args, '--grid' ) if $grid;
    push( @args, '--dagman',$dagman, $config{dag} );

    print STDERR "# ", join(' ',@args), "\n" if $main::DEBUG;
    system(@args) == 0 
	or die( "ERROR: Running pegasus-submit-dag failed with ", parse_exit($?));
    print STDERR "# dagman is running\n" if $main::DEBUG;

    if ( $monitor ) {

    if ( -x $tsd ) {
	my @tsdargs = ($tsd);
	# run pegasus-monitord to update job stats until DAGMan finishes
	if(!$database){
	    #@args=create_args( $tsd,'--nodatabase', $config{dag} . '.dagman.out' );
	    push(@tsdargs,'--no-database');
	    push(@tsdargs, $config{dag} . '.dagman.out' );
	} else {
	    #@args = create_args( $tsd, ,"-n", $config{dag} . '.dagman.out' );
	    push(@tsdargs, $config{dag} . '.dagman.out' );
	}
	print STDERR "# ", join(' ',@tsdargs), "\n" if $main::DEBUG;
	system(@tsdargs) == 0 
	    or warn( "WARN: Running $tsd failed with ", parse_exit($?) );
	print STDERR "# $tsd is running\n" if $main::DEBUG;
    }

    }
    # next step
    if ( @rescue > 0 ) {
	my $n = @rescue + 0;
	print( "\nI found $n rescue DAG", ( $n>1 ? 's' : '' ), 
	       ". I submitted the rescue DAG\n", 
	       $config{dag}, "\ninstead of\n", $original, "\n" );
    }

    my $did=undef;
    my $daglogfile=$config{dag}.".dagman.out";
    if ( open( DID, "<$daglogfile" ) ) {
	while (<DID>) {
	    if ( /condor_scheduniv_exec/ ) {
		# this part was written by a python programmer?
		$did=(split /\./,  (split)[3],2)[1];
		last;
	    }
	}
	close(DID);
    }
    print << "EOF";

Your Workflow has been started and runs in base directory given below

cd $run

*** To monitor the workflow you can run ***

pegasus-status -l $run

*** To remove your workflow run ***
EOF

    print "pegasus-remove -d $did\nor\n" if defined $did; 
    print "pegasus-remove $run\n\n";

} elsif ( $config{type}=="shell" ) {
    # sanity check: Is the SCRIPT file there? 
    die "ERROR: Unable to execute $config{script}\n" unless -x $config{script};
    print STDERR "# found $config{script}\n" if $main::DEBUG;

    my @args=( "/bin/bash", $config{script} );
    system(@args) == 0 
	or die( "ERROR: Running $config{script} failed with ", 
		parse_exit($?) );
}

chdir($here);
exit 0;
